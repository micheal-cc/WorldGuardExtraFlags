plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.0'
    id 'kr.entree.spigradle' version '2.4.2'
    id "net.kyori.indra.git" version "2.1.1"
}

group 'io.github.invvk'
version "$parent.version"

repositories {
    mavenCentral()
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

configurations {
    embed
}

dependencies {
    compileOnly 'net.essentialsx:EssentialsX:2.19.4'
    compileOnly 'me.clip:placeholderapi:2.11.6'

    compileOnly "org.bstats:bstats-bukkit:3.0.0"
    embed "org.bstats:bstats-bukkit:3.0.0"

    compileOnly project(":WGEF-Abstraction")
    embed project(":WGEF-Abstraction")

    compileOnly project(":WG7")
    embed project(":WG7")
}

shadowJar {
    configurations = [project.configurations.embed]
    
    archiveBaseName.set('WGEF-REBORN')
    archiveClassifier.set('')
    archiveVersion.set('')

    exclude 'META-INF/**'
    exclude 'LICENSE'

    relocate 'org.bstats', 'io.github.invvk.wgef.metrics'
}

tasks.named("generateSpigotDescription") {
    doLast {
        def pluginYml = file("${buildDir}/resources/main/plugin.yml")
        if (pluginYml.exists()) {
            def lines = pluginYml.readLines()
            def newLines = lines.findAll { !it.startsWith("libraries:") && !it.trim().startsWith("- io.github.invvk") && !it.trim().startsWith("- org.bstats") }
            pluginYml.text = newLines.join("\n")
        }
    }
}

spigot {
    main 'io.github.invvk.wgef.WGEFPlugin'
    name 'WGEF-Reborn'
    authors 'Invvk', 'isokissa3'
    apiVersion '1.17'
    version "${parent.version}+${indraGit.commit().abbreviate(7).name()}"
    depends 'WorldGuard'
    softDepends 'FastAsyncWorldEdit', 'Essentials', 'TAB'
    description 'Adds more flags to WorldGuard to help manage your server easily!'
}

afterEvaluate {
    repositories.removeIf { it instanceof MavenArtifactRepository && it.url.toString().contains("papermc.io") }
    tasks.named('detectSpigotMain').configure {
        enabled = false
    }
}